apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.timfreiheit.resourceplaceholders'

static def releaseTime() {
    return new Date().format("yy.MMddHH", TimeZone.getTimeZone("GMT+8"))
}

def name = "legado"
def version = "3." + releaseTime()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 30
    signingConfigs {
        if (project.hasProperty("RELEASE_STORE_FILE")) {
            myConfig {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }
    defaultConfig {
        applicationId "io.legado.app"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode gitCommits
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        project.ext.set("archivesBaseName", name + "_" + version)
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.incremental"     : "true",
                        "room.expandProjection": "true",
                        "room.schemaLocation"  : "$projectDir/schemas".toString()
                ]
            }
        }
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.myConfig
            }
            applicationIdSuffix '.release'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.myConfig
            }
            applicationIdSuffix '.debug'
            versionNameSuffix 'debug'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                def flavor = variant.productFlavors[0].name
                outputFileName = "${name}_${flavor}_${defaultConfig.versionName}.apk"
            }
        }
    }
    flavorDimensions "mode"
    productFlavors {
        app {
            dimension "mode"
            manifestPlaceholders = [APP_CHANNEL_VALUE: "app"]
        }
        google {
            dimension "mode"
            applicationId "io.legado.play"
            manifestPlaceholders = [APP_CHANNEL_VALUE: "google"]
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildToolsVersion '30.0.3'
}

resourcePlaceholders {
    files = ['xml/shortcuts.xml']
}

kapt {
    arguments {
        arg("room.schemaLocation", "$projectDir/schemas")
    }
}

dependencies {
    implementation project(path: ':epublib')
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation fileTree(dir: 'libs', include: ['*.jar','*.aar'])
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    //androidX
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "androidx.activity:activity-ktx:1.2.3"
    implementation "androidx.fragment:fragment-ktx:1.3.3"
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.collection:collection-ktx:1.1.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.google.android:flexbox:1.1.0'
    implementation 'com.google.code.gson:gson:2.8.6'

    //media
    def media2_version = "1.1.3"
    implementation "androidx.media2:media2-session:$media2_version"
    implementation "androidx.media:media:1.3.1"
    //implementation "androidx.media2:media2-player:$media2_version"
    //implementation 'com.google.android.exoplayer:exoplayer:2.13.0'

    //Splitties
    def splitties_version = '2.1.1'
    implementation("com.louiscad.splitties:splitties-appctx:$splitties_version")
    implementation("com.louiscad.splitties:splitties-systemservices:$splitties_version")
    implementation("com.louiscad.splitties:splitties-views:$splitties_version")

    //lifecycle
    def lifecycle_version = '2.3.1'
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    //room
    def room_version = '2.3.0'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    //paging
    implementation 'androidx.paging:paging-runtime-ktx:3.0.0'

    //liveEventBus
    implementation 'com.jeremyliao:live-event-bus-x:1.5.7'

    //协程
    def coroutines_version = '1.4.3'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    //规则相关
    implementation 'org.jsoup:jsoup:1.13.1'
    //noinspection GradleDependency
    implementation 'cn.wanghaomiao:JsoupXpath:2.3.2'
    implementation 'com.jayway.jsonpath:json-path:2.5.0'

    //JS rhino
    implementation 'com.github.gedoor:rhino-android:1.5'

    //网络
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'

    //webServer
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'org.nanohttpd:nanohttpd-websocket:2.3.1'

    //二维码
    implementation 'com.king.zxing:zxing-lite:2.0.3'

    //颜色选择
    implementation 'com.jaredrummler:colorpicker:1.1.0'

    //apache
    implementation 'org.apache.commons:commons-text:1.9'

    //MarkDown
    implementation 'io.noties.markwon:core:4.6.0'
    implementation 'io.noties.markwon:image-glide:4.6.0'
    implementation 'io.noties.markwon:ext-tables:4.6.0'
    implementation 'io.noties.markwon:html:4.6.0'

    //转换繁体
    implementation 'com.github.liuyueyi.quick-chinese-transfer:quick-transfer-core:0.1.3'

}